CLICON_MODE="operation";
#CLICON_PROMPT="%U@%H %W> ";
CLICON_PROMPT="%U@%H> ";
CLICON_PLUGIN="controller_cli";

# Al11: Auto edit mode
# Autocli syntax tree operations
configure("Change to configure mode"), cli_set_mode("configure");
exit("Quit"), cli_quit();
quit("Quit"), cli_quit();

debug("Debugging parts of the system") {
    level("Set debug level: 1..n") <level:int32>("Set debug level (0..n)"), cli_debug_backend();
    cli("Set cli debug")	 <level:int32>("Set debug level (0..n)"), cli_debug_cli();
    backend("Set backend debug") <level:int32>("Set debug level (0..n)"), cli_debug_backend();
    restconf("Set restconf debug") <level:int32>("Set debug level (0..n)"), cli_debug_restconf();
}

default("Set CLI default values") {
   format("Set default output format") <fmt:string choice:xml|json|text|cli>("CLI output format"), cli_format_set();
}
shell("System command"), cli_start_shell("bash");{
  <source:rest>("Single shell command"), cli_start_shell("bash");
}
processes("Process maintenace") services("Service daemon operation") {
    start, cli_process_control("Action process", "start");
    stop, cli_process_control("Action process", "stop");
    restart, cli_process_control("Action process", "restart");
    status, cli_process_control("Action process", "status");
}
show("Show a particular state of the system"){
    compare("Compare candidate and running databases"), compare_dbs_rpc("running", "candidate", "default");{
       xml("Show comparison in xml"), compare_dbs_rpc("running", "candidate","xml");
       text("Show comparison in text"), compare_dbs_rpc("running", "candidate", "text");
    }
    configuration("Show configuration"), cli_show_auto_mode("running", "default", true, false);{
       @|pipe_show, cli_show_auto_mode("running", "xml", true, false);
       @datamodelshow, cli_show_auto_devs("running", "default", true, false, "explicit", "set ", true);
    }
    connections("Show state of connection to devices")[
                 (<name:string>("device pattern")|
                  <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern"))
                 ], cli_show_connections();{
                 @|pipe_common, cli_show_connections();
             }
    devices("Show state of devices")[
                 (<name:string>("device pattern")|
                  <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern"))
                 ] {
                 check("Check if device is in sync"), check_device_db("default");
                 diff("Compare remote device config with local"), compare_device_db_dev("default");
                 capability("Show device capbilities"), show_device_capability("name"); {
                    @|pipe_show, show_device_capability("name");
                 }
                 yang("Show yang revisions"), show_yang_revisions("name"); {
                    @|pipe_show, show_yang_revisions("name");
                 }
             }
    options("Show clixon options"), cli_show_options();{
       @|pipe_show, cli_show_options();
    }
    services("Show service process status"), cli_show_services_process();{
       detail("Show detailed process status"), cli_process_control("Action process", "status");
    }
    sessions("Show client sessions"), cli_show_sessions();{
         detail("Show sessions detailed state"), cli_show_sessions("detail");
    }
    state("Show configuration and state"), cli_show_auto_mode("running", "default", true, true); {
        @|pipe_show, cli_show_auto_mode("running", "default", true, true);
        xml("Show configuration and state as XML"), cli_show_auto_mode("running", "xml", true, true);{
	    default("With-default mode"){
                  report-all, cli_show_auto_mode("running", "default", true, true, "report-all");
		  trim, cli_show_auto_mode("running", "default", true, true, "trim");
		  explicit, cli_show_auto_mode("running", "default", true, true, "explicit");
		  report-all-tagged, cli_show_auto_mode("running", "default", true, true, "report-all-tagged");
		  report-all-tagged-default, cli_show_auto_mode("running", "default", true, true, "report-all-tagged-default");
		  report-all-tagged-strip, cli_show_auto_mode("running", "default", true, true, "report-all-tagged-strip");
	    }
        }
    }
    memory("Show memory usage"), cli_show_statistics("all");{
       cli, cli_show_statistics("cli");{
          detail, cli_show_statistics("cli", "detail");{
             @|pipe_common, cli_show_statistics("cli", "detail");
          }
       }
       backend, cli_show_statistics("backend"); {
          detail, cli_show_statistics("backend", "detail");
          @|pipe_common, cli_show_statistics("backend");
       }
    }
    transactions("Show state of last transaction"), cli_show_transactions("last");{
         last("Last most recent transactions"), cli_show_transactions("last");
         all("All transactions"), cli_show_transactions("all");
    }
    version("Show version"), cli_controller_show_version();
    xpath("Show configuration") <xpath:string>("XPATH expression")
       [<ns:string>("Namespace")], show_conf_xpath("running");{
          @|pipe_show, show_conf_xpath("running");
       }
}

save("Save running configuration to XML file") <filename:string>("Filename (local filename)"), save_config_file("running","filename", "xml");{
    cli("Save configuration as CLI commands"), save_config_file("running","filename", "cli");
    xml("Save configuration as XML"), save_config_file("running","filename", "xml");
    json("Save configuration as JSON"), save_config_file("running","filename", "json");
    text("Save configuration as TEXT"), save_config_file("running","filename", "text");
    netconf("Save configuration as NETCONF"), save_config_file("running","filename", "netconf");
}

pull("Pull config from one or multiple devices")[
                 (<name:string>("device pattern")|
                  <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern"))
                 ], cli_rpc_pull("replace");{
                    replace, cli_rpc_pull("replace");
                    merge, cli_rpc_pull("merge");
}
push("Push config to one or multiple devices")[
                 (<name:string>("device pattern")|
                  <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern"))
                 ], cli_rpc_controller_commit("running", "NONE", "COMMIT");{
                    validate("Push to devices and validate"), cli_rpc_controller_commit("running", "NONE", "VALIDATE");
                    commit("Push to devices and commit"), cli_rpc_controller_commit("running", "NONE", "COMMIT");
}
connection("Change connection state of one or several devices") {
   close("Close open connections"), cli_connection_change("CLOSE", false);{
      (<name:string>("device pattern")|
       <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")), cli_connection_change("CLOSE", false);
   }
   open("Open closed connections"), cli_connection_change("OPEN", true);{
      async("Open connection do not block"), cli_connection_change("OPEN", false);
      (<name:string>("device pattern")|
       <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")), cli_connection_change("OPEN", true);
   }
   reconnect("Close all open connections and open all connections"), cli_connection_change("RECONNECT", true);{
      async("Do not block"), cli_connection_change("RECONNECT", false);
      (<name:string>("device pattern")|
       <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")), cli_connection_change("RECONNECT", true);{
          async("Do not block"), cli_connection_change("RECONNECT", false);
      }
   }
}
session("Client sessions") {
    kill("Kill client session") {
           <session:uint32 choice:1677721>("Transaction number"), cli_kill_session("session");
           <session:uint32>("Client session number"), cli_kill_session("session");
    }
}

