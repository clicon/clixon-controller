CLICON_MODE="operation";
#CLICON_PROMPT="%U@%H %W> ";
CLICON_PROMPT="%U@%H> ";
CLICON_PLUGIN="controller_cli";

# Al11: Auto edit mode
# Autocli syntax tree operations
configure("Change to configure mode"), cli_set_mode("configure");
exit("Quit"), cli_quit();
quit("Quit"), cli_quit();

debug("Debugging system"){
    cli("Set cli debug")	 (<level:uint32>("Set debug level")|
                                  <level:string expand_yang_bits("/cc:clixon-config/cc:CLICON_DEBUG")>("Set debug level")), cli_debug_cli("/cc:clixon-config/cc:CLICON_DEBUG");
    backend("Set backend debug") (<level:uint32>("Set debug level (0..n)")|
                                  <level:string expand_yang_bits("/cc:clixon-config/cc:CLICON_DEBUG")>("Set debug level")), cli_debug_backend("/cc:clixon-config/cc:CLICON_DEBUG");
    restconf("Set restconf debug") (<level:uint32>("Set debug level (0..n)")|
                                    <level:string expand_yang_bits("/cc:clixon-config/cc:CLICON_DEBUG")>("Set debug level")), cli_debug_restconf("/cc:clixon-config/cc:CLICON_DEBUG");
}
default("Set CLI default values") {
   format("Set default output format") <fmt:string choice:xml|json|text|cli>("CLI output format"), cli_format_set();
}
processes("Process maintenace") {
   services("Service daemon operation") {
      start, cli_process_control("Services process", "start");
      stop, cli_process_control("Services process", "stop");
      restart, cli_process_control("Services process", "restart");
      status, cli_process_control("Services process", "status");
   }
   restconf("RESTCONF daemon operations") {
      start, cli_process_control("restconf", "start");
      stop, cli_process_control("restconf", "stop");
      restart, cli_process_control("restconf", "restart");
      status, cli_process_control("restconf", "status");
   }
}
show("Show a particular state of the system"){
    compare("Compare candidate and running databases"), compare_dbs_rpc("running", "candidate", "default");{
       xml("Show comparison in xml"), compare_dbs_rpc("running", "candidate","xml");
#       text("Show comparison in text"), compare_dbs_rpc("running", "candidate", "text");
    }
    detail("Show details about a configured node: yang, namespaces, etc"){
        @basemodel, @remove:act-list, cli_show_config_detail();
    }
    configuration("Show configuration"), cli_show_auto_mode("running", "default", true, false);{
       @|pipe_show, cli_show_auto_mode("running", "xml", true, false);
       @datamodelshow, cli_show_auto_devs("running", "pipe-xml-default", true, false, "explicit", "set ", true);
    }
    connections("Show state of connection to devices")[
                 (<name:string>("device pattern")|
                  <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern"))
                 ], cli_show_connections();{
                 @|pipe_common, cli_show_connections();
                 detail("Show detailed connection status"), cli_show_connections("detail");{
                    @|pipe_show, cli_show_connections("detail");
                 }
             }
    devices("Show state of devices"){
       [(<name:string>("device pattern")
        |<name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern"))] {
        @controller_show_devices;
       }
       <group:string keyword:group>("device group")
          (<name:string>("device group pattern")
           |<name:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern")) {
          @controller_show_devices;
       }
    }
    options("Show clixon options"), cli_show_options();{
       @|pipe_show, cli_show_options();
    }
    services("Show service process status"), cli_show_services_process();{
       detail("Show detailed process status"), cli_process_control("Services process", "status");
    }
    sessions("Show client sessions"), cli_show_sessions();{
         detail("Show sessions detailed state"), cli_show_sessions("detail");
    }
    state("Show configuration and state"), cli_show_auto_mode("running", "default", true, true);{
       @|pipe_show, cli_show_auto_mode("running", "xml", true, true);
       @datamodelstate, cli_show_auto_devs("running", "pipe-xml-default", true, true, "explicit", "set ", true);
    }
    memory("Show memory usage"), cli_show_statistics("all");{
       cli, cli_show_statistics("cli");{
          detail, cli_show_statistics("cli", "detail");{
             @|pipe_common, cli_show_statistics("cli", "detail");
          }
       }
       backend, cli_show_statistics("backend"); {
          detail, cli_show_statistics("backend", "detail");
          @|pipe_common, cli_show_statistics("backend");
       }
    }
    transactions("Show state of last transaction"), cli_show_transactions("last");{
         last("Last most recent transactions"), cli_show_transactions("last");
         all("All transactions"), cli_show_transactions("all");
    }
    version("Show version"), cli_controller_show_version();
    xpath("Show configuration") <xpath:string>("XPATH expression")
       [<ns:string>("Namespace")], show_conf_xpath("running");{
          @|pipe_show, show_conf_xpath("running");
       }
}

save("Save running configuration to XML file") <filename:string>("Filename (local filename)"), save_config_file("running","filename", "xml");{
    cli("Save configuration as CLI commands"), save_config_file("running","filename", "cli");
    xml("Save configuration as XML"), save_config_file("running","filename", "xml");
    json("Save configuration as JSON"), save_config_file("running","filename", "json");
    text("Save configuration as TEXT"), save_config_file("running","filename", "text");
    netconf("Save configuration as NETCONF"), save_config_file("running","filename", "netconf");
}
pull("Pull config from one or multiple devices"){
   [(<name:string>("device pattern")
   |<name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern"))
   ], cli_rpc_pull("replace");{
      replace, cli_rpc_pull("replace");
      merge, cli_rpc_pull("merge");
      diff, compare_device_db_dev("default");
      check("Check if device is in sync"), check_device_db("default");
   }
   <group:string keyword:group>("device group")
         (<name:string>("device group pattern")
          |<name:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern"))
         , cli_rpc_pull("replace");{
      replace, cli_rpc_pull("replace");
      merge, cli_rpc_pull("merge");
      diff, compare_device_db_dev("default");
      check("Check if device is in sync"), check_device_db("default");
   }
}
push("Push config to one or multiple devices"){
   [(<name:string>("device pattern")
    |<name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern"))
   ], cli_rpc_controller_commit("running", "NONE", "COMMIT");{
      validate("Push to devices and validate"), cli_rpc_controller_commit("running", "NONE", "VALIDATE");     commit("Push to devices and commit"), cli_rpc_controller_commit("running", "NONE", "COMMIT");
   }
   <group:string keyword:group>("device group")
         (<name:string>("device group pattern")
          |<name:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern"))
         , cli_rpc_controller_commit("running", "NONE", "COMMIT");{
      validate("Push to devices and validate"), cli_rpc_controller_commit("running", "NONE", "VALIDATE");     commit("Push to devices and commit"), cli_rpc_controller_commit("running", "NONE", "COMMIT");
   }
}
connection("Change connection state of one or several devices") {
   close("Close open connections"), cli_connection_change("CLOSE");{
      (<name:string>("device pattern")
      |<name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")
       ), cli_connection_change("CLOSE");
      <group:string keyword:group>("device group")
          (<name:string>("device group pattern")
           |<name:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern"))
          , cli_connection_change("CLOSE");
   }
   open("Open closed connections"), cli_connection_change("OPEN"); {
      <async:string keyword:async>("Return immediately, do not wait for completion"), cli_connection_change("OPEN"); {
         (<name:string>("device pattern") |
          <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")
         ), cli_connection_change("OPEN");
         <group:string keyword:group>("device group")
            (<name:string>("device group pattern")
            |<name:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern"))
             , cli_connection_change("OPEN");
      }
      (<name:string>("device pattern") |
       <name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")
      ), cli_connection_change("OPEN");
      <group:string keyword:group>("device group")
          (<name:string>("device group pattern")
           |<name:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern")), cli_connection_change("OPEN");

   }
   reconnect("Close all open connections and open all connections"), cli_connection_change("RECONNECT");{
      <async:string keyword:async>("Return immediately, do not wait for completion"), cli_connection_change("OPEN"); {
         (<name:string>("device pattern")
         |<name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")
         ), cli_connection_change("RECONNECT");
         <group:string keyword:group>("device group")
              (<name:string>("device group pattern")
               |<name:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern")
               ), cli_connection_change("RECONNECT");
      }
      (<name:string>("device pattern")
       |<name:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")
      ), cli_connection_change("RECONNECT");
      <group:string keyword:group>("device group")
           (<name:string>("device group pattern")
            |<name:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern")
            ), cli_connection_change("RECONNECT");
   }
}
rpc("Send RPC to devices")
   <templ:string expand_dbvar("running","/clixon-controller:devices/rpc-template/name")>("RPC template to apply") {
      (<devname:string>("device pattern")|
       <devname:string expand_dbvar("running","/clixon-controller:devices/device/name")>("device pattern")), cli_device_rpc_template("templ", "devname");{
          variables @rpc_template_vars; # see controller_rpc_template_vars.cli
   }
   <group:string keyword:group>("device group")
      (<devname:string>("device group pattern")
      |<devname:string expand_dbvar("running","/clixon-controller:devices/device-group/name")>("device-group pattern")
      ), cli_device_rpc_template("templ", "devname");{
          variables @rpc_template_vars; # see controller_rpc_template_vars.cli
      }
}
session("Client sessions") {
    kill("Kill client session") {
           <session:uint32 choice:1677721>("Transaction number"), cli_kill_session("session");
           <session:uint32>("Client session number"), cli_kill_session("session");
    }
}
